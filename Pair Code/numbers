use bool
use list
use calls

NaN is T

# list of short numbers
1 is list T 0
2 is pair 0 1
3 is pair T 1
4 is pair 0 2
5 is pair T 2
6 is pair 0 3
7 is pair T 3
8 is pair 0 4
9 is pair T 4
10 is pair 0 5
11 is pair T 5
12 is pair 0 6
13 is pair T 6
14 is pair 0 7
15 is pair T 7
16 is pair 0 8
17 is pair T 8
18 is pair 0 9
19 is pair T 9
20 is pair 0 10

inc x is 
	if x
	then if head x
		then list false inc tail x
		else list true tail x
	else 1

dec x is
	if tail x
	then if head x
		then list false tail x
		else list true dec tail x
	else 0

sum x y is 
	if x
	then if y
		then list 
			xor head x head y
			if both head x head y
				inc sum tail x tail y
				sum tail x tail y
		else x
	else y

shorten x is
	if neither head x tail x
	then nil
	else x

normalize x is
	both x
	shorten pair head x normalize tail x

subtract x y is
	if not y x
	shorten pair
		xor head x head y
		if both not head x head y
			then dec subtract tail x tail y
			else subtract tail x tail y

half x is tail x

double x is
	both x
	pair 0 x

mult x y is
	both y
	if head y
		sum double mult x half y x
	else double mult x half y

sqr x is mult x x
cb x is mult mult x x x

pow x y is
	if not y then 1
	else mult pow x dec y x

fastpow x y is
	if not y then 1
	else if head y 
		then mult x sqr fastpow x half y 
		else sqr fastpow x half y

< is pair 0 T
> is pair T 0
= is 0

compare x y is
	if x if y 
		either compare second x second y compare first x first y
	else > else if y then < else =

less x y is second compare x y
greater x y is first compare x y
equal x y is not compare x y
lessOrEqual x y is not greater x y
greaterOrEqual x y is not less x y
	
between x a b is
	both greaterOrEqual x a lessOrEqual x b

<= is pair 0 2
>= is pair 2 0

cmp x s y is
	either eq s compare x y
	either both eq s <= lessOrEqual x y
	both eq s >= greaterOrEqual x y

log2 x is
	if less x 2 then 0
	else inc log2 half x

divideInner x y p2 is
	if less second p2 y
		pair double first p2 second p2
		pair inc double first p2 subtract second p2 y

divideB x y is
	if less x y 
		pair 0 x
		divideInner x y divideB x double y

divB x y is first divideB x y
modB x y is second divideB x y

normalizeDivide m d is
	if less second d m then d
	pair inc first d subtract second d m

doubleDivide m d is
	pair double first d double second d

incDivide m d is
	pair first d inc second d

divide x y is
	if not x then pair 0 0
	if head x
	then normalizeDivide y incDivide y doubleDivide y divide tail x y
	else normalizeDivide y doubleDivide y divide tail x y

div x y is first divide x y
mod x y is second divide x y

gcd x y is
	if not y x
	gcd y mod x y

inf is pair true inf

# higher order functions

multipleApply f n is 
	if eq n 0 return function x to x
	return function x to 
		call1 multipleApply f dec n 
			call1 f x

binarySearch left right big is
	if eq left right left
	give half inc sum left right function c to
	if call1 big c
	then binarySearch left dec c big
	else binarySearch c right big

sqrt x is binarySearch 0 x function y to less x sqr y

cbrt x is binarySearch 0 x function y to less x cb y

divbs x y is binarySearch 0 x function z to less x mult y z
modbs x y is subtract x mult y divbs x y

gcdbs x y is 
	if not x y
	gcdbs modbs y x x

d2 a b is sum mult a 10 b
d3 a b c is sum mult d2 a b 10 c
d4 a b c d is sum mult d3 a b c 10 d
d5 a b c d e is sum mult d4 a b c d 10 e
d6 a b c d e f is sum mult d5 a b c d e 10 f

positionalNumberBase list base result is
	if not list then result
	else positionalNumberBase 
		tail list
		base
		sum mult result base head list 

positional list base is positionalNumberBase list base 0
num list is positional list 10

doubleBase x is 
	map x uncall double

incBase x is
	if x pair inc head x tail x
	pair 1 nil

normalizeBase x base is
	both x
	if less head x base 
	then pair head x normalizeBase tail x base
	else pair subtract head x base normalizeBase incBase tail x base
 
toPositional n base is
	both n
	if head n 
	normalizeBase incBase doubleBase toPositional tail n base base
	else normalizeBase doubleBase toPositional tail n base base

toPositionalD n base is
	both n
	give divide n base function p to
	pair second p toPositionalD first p base
